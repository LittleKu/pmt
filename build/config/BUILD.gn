import("//build/config/c++/c++.gni")

config("debug") {
  defines = [ "_DEBUG" ]

  if (is_win) {
    if (!enable_iterator_debugging) {
      defines += [ "_HAS_ITERATOR_DEBUGGING=0" ]
    }
  }
}

config("release") {
  defines = [ "NDEBUG" ]
}

config("default_libs") {
  if (is_win) {
    libs = [
      "advapi32.lib",
      "comdlg32.lib",
      "dbghelp.lib",
      "dnsapi.lib",
      "gdi32.lib",
      "msimg32.lib",
      "odbc32.lib",
      "odbccp32.lib",
      "oleaut32.lib",
      "shell32.lib",
      "shlwapi.lib",
      "user32.lib",
      "usp10.lib",
      "uuid.lib",
      "version.lib",
      "wininet.lib",
      "winmm.lib",
      "winspool.lib",
      "ws2_32.lib",
    ]

    if (current_os == "winuwp") {
      # These libraries are needed for Windows UWP (i.e. store apps).
      libs += [
        "dloadhelper.lib",
        "WindowsApp.lib",
      ]
    } else {
      # These libraries are not compatible with Windows UWP (i.e. store apps.)
      libs += [
        "delayimp.lib",
        "kernel32.lib",
        "ole32.lib",
      ]
    }
  }
}

if (is_win) {
  _windows_linker_configs = [
    "//build/config/win:sdk_link",
    "//build/config/win:common_linker_setup",
  ]
}
config("executable_config") {
  configs = []

  if (is_win) {
    configs += _windows_linker_configs
  }
}

config("shared_library_config") {
  configs = []

  if (is_win) {
    configs += _windows_linker_configs
  }
}
